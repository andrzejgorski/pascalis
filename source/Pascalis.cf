    comment "//" ;
    comment "{" "}" ;

    Prog. Program   ::= "program" Ident ";" [Decl] "incipe" [Stm] "fini." ;
    DVar.  Decl     ::= "variabilis" Ident ":" Type ;
    DParam. Decl    ::= "param" Ident ":" Type ;
    DAVar. Decl     ::= "variabilis" Ident ":" "matrix" "[" Exp ".." Exp"]" "autem" Type ;
    DAPVar. Decl    ::= "variabilis" Ident ":" "matrix" "{" Type "}" "autem" Type ;
    DProc. Decl     ::= "processus" Ident "(" [Decl] ")" ";" [Decl] "incipe" [Stm] "fini" ;
    DFunc. Decl     ::= "functio" Ident "(" [Decl] ")" ":" Type ";" [Decl] "incipe" [Stm] "fini" ;

	terminator Stm "" ;
	separator  Decl ";" ;
	separator  nonempty Ident "," ;

    Skip.    Stm ::= "persulta" ";" ;

    SIf.     Stm ::= "si" Exp "tunc" Stm ;
    SIfElse. Stm ::= "si" Exp "tunc" Stm "alter" Stm;
	SExp.    Stm ::= Exp ";" ;
    SDecl.   Stm ::= [Decl] ;
	SBlock.  Stm ::= "incipe" [Stm] "fini" ";" ;
	SReturn. Stm ::= "refer" Exp ";" ;
	STSet.   Stm ::= Ident "[" Exp "]" ":=" Exp ";" ;
	SSet.    Stm ::= Ident ":=" Exp ";" ;
	SWhile.  Stm ::= "donec" Exp "fac" Stm ;
	SFor.    Stm ::= "pro" Ident "=" Exp "ut" Exp "fac" Stm ;

	BTrue.   Exp ::= "verum" ;
	BFalse.  Exp ::= "falsum" ;
	Null.    Exp ::= "nullum" ;
	BNot.    Exp ::= "non" Exp ;

	EFSub.   Exp ::= Exp4 "[:]" ;
	ELSub.   Exp ::= Exp4 "[" Exp ":]" ;
	ERSub.   Exp ::= Exp4 "[:" Exp "]" ;
	ELRSub.  Exp ::= Exp4 "[" Exp ":" Exp "]" ;
	EKey.    Exp ::= Exp4 "[" Exp "]" ;
	-- ELen.    Exp ::= "longitudo" "(" Exp ")" ;
	-- EOrd.    Exp ::= "ord" "(" Exp ")" ;

    EOr.     Exp ::= Exp "uel" Exp2 ;
    EAnd.    Exp ::= Exp "et" Exp2 ;
	EAss.    Exp ::= Exp "=" Exp2 ;
	ENAss.   Exp ::= Exp "<>" Exp2 ;
	ELt.     Exp ::= Exp2 "<" Exp3 ;
	EGt.     Exp ::= Exp2 ">" Exp3 ;
	ELEt.    Exp ::= Exp2 "=<" Exp3 ;
	EGEt.    Exp ::= Exp2 ">=" Exp3 ;

	EAdd.    Exp2 ::= Exp2 "+" Exp3 ;
	ESub.    Exp2 ::= Exp2 "-" Exp3 ;
	EMul.    Exp3 ::= Exp3 "*" Exp4 ;
	EDiv.    Exp3 ::= Exp3 "/" Exp4 ;

	Call.    Exp4 ::= Ident "(" [Exp] ")" ;

	EStr.    Exp4 ::= String ;
	EChar.   Exp4 ::= Char ;
	EVar.    Exp4 ::= Ident ;
	EInt.    Exp4 ::= Integer ;
	EDouble. Exp4 ::= Double ;

	coercions Exp 4 ;

	separator Exp "," ;

    TInt.   Type ::= "numeri integri" ;
    TBool.  Type ::= "logica booleana" ;
    TStr.   Type ::= "titulus" ;
    TChar.  Type ::= "litera" ;
    TFunc.  Type ::= "functio" ;
    TProc.  Type ::= "procedure" ;
    TArr.   Type ::= "matrix" "{" Type "}" "autem" Type ;
    TDict.  Type ::= "dictionarum" "{" Type "}" "autem" Type ;
